// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1


/*
Kevin Ho kth258
So its a whole lot but basically you start by zero and/or negating the zero output if it is 
0 or 1 and then apply the funcitons selector, after that you can then determine the 
zr and ng terms with 8way and an Negation of the last digit for the output

*/
CHIP ALU {
    IN  
        x[16], y[16],        
        zx, nx, zy, ny, f, no;

    OUT 
        out[16], zr, ng;

    PARTS:
    // Zero the x and y inputs if zx or zy is 1
    Mux16(a=x, b=false, sel=zx, out=xZero);
    Mux16(a=y, b=false, sel=zy, out=yZero);

    // Negate x and y inputs if nx or ny is 1
    Not16(in=xZero, out=xNegated);
    Mux16(a=xZero, b=xNegated, sel=nx, out=xPro);

    Not16(in=yZero, out=yNegated);
    Mux16(a=yZero, b=yNegated, sel=ny, out=yPro);

    // Dertermines which function to output to add or apply And
    Add16(a=xPro, b=yPro, out=addRes);
    And16(a=xPro, b=yPro, out=andRes);
    Mux16(a=andRes, b=addRes, sel=f, out=aout);

    // Negate the output if no is 1
    Not16(in=aout, out=bout);
    Mux16(a=aout, b=bout, sel=no, out=out, out[15] = ng, out[0..7] = lowerOut, out[8..15] = upperOut);
    Or8Way(in=lowerOut, out=checkLower);
    Or8Way(in=upperOut, out=checkUpper);
    Or(a=checkLower, b=checkUpper, out=notZr);
    Not(in=notZr, out=zr);
}


